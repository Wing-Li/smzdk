apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'io.objectbox'
apply plugin: 'kotlin-kapt'

//加载资源
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
properties.load(inputStream)

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.lyl.smzdk"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        flavorDimensions "100"

        multiDexEnabled true

        buildConfigField("String", "BuglyKey", properties.getProperty('BuglyKey'))
    }

    apply from: 'signingConfigs.gradle', to: android

    buildTypes {
        debug {
            applicationIdSuffix = '.debug'

            resValue("string", "app_name", "值得看-DEV")

            buildConfigField("String", "URL_MYAPI", '"http://127.0.0.1:8080/api/"')
            buildConfigField("String", "MYAPI_MN", '"http://mvimg.lylyl.cn:8089/api/"')
            buildConfigField("String", "IMAGE_BASE_URL", '"http://qnimg.lylyl.cn/"')
            buildConfigField("String", "QiniuAK", properties.getProperty('QiniuAK'))
            buildConfigField("String", "QiniuSK", properties.getProperty('QiniuSK'))
            buildConfigField("String", "QiniuBucket", '"lyl-smzdk-stage"')

            buildConfigField "String", "Environment", '"dev"'

            signingConfig signingConfigs.stage
            debuggable true
        }

        stage {
            applicationIdSuffix = '.stage'

            resValue("string", "app_name", "值得看-STAGE")

            buildConfigField("String", "URL_MYAPI", '"http://smzdk.lylyl.cn:8010/api/"')
            buildConfigField("String", "MYAPI_MN", '"http://mvimg.lylyl.cn:8089/api/"')
            buildConfigField("String", "IMAGE_BASE_URL", '"http://qnimg.lylyl.cn/"')
            buildConfigField("String", "QiniuAK", properties.getProperty('QiniuAK'))
            buildConfigField("String", "QiniuSK", properties.getProperty('QiniuSK'))
            buildConfigField("String", "QiniuBucket", '"lyl-smzdk-stage"')

            buildConfigField "String", "Environment", '"dev"'

            signingConfig signingConfigs.stage
            debuggable true
        }

        release {
            resValue("string", "app_name", "值得看")

            buildConfigField("String", "URL_MYAPI", '"http://smzdk.lylyl.cn:8015/api/"')
            buildConfigField("String", "MYAPI_MN", '"http://mvimg.lylyl.cn:8089/api/"')
            buildConfigField("String", "IMAGE_BASE_URL", '"http://qnimg.lylyl.cn/"')
            buildConfigField("String", "QiniuAK", properties.getProperty('QiniuAK'))
            buildConfigField("String", "QiniuSK", properties.getProperty('QiniuSK'))
            buildConfigField("String", "QiniuBucket", '"lyl-smzdk"')

            buildConfigField "String", "Environment", '"prod"'

            signingConfig signingConfigs.release
            minifyEnabled false // 混淆打包
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 批量打包
            applicationVariants.all { variant ->
                variant.outputs.all {
                    //outputFileName = "${variant.productFlavors[0].name}_v${signingConfigs.versionName}_${releaseTime()}.apk"
                    outputFileName = "smzdk_v${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize = "4g"
    }
}

kapt {
    generateStubs true
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'

    // 框架
    implementation 'com.yanzhenjie:permission:2.0.0-rc11'
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    implementation 'com.facebook.fresco:fresco:1.9.0'
    implementation 'com.facebook.fresco:animated-gif:1.9.0'

    // 控件
    implementation 'me.majiajie:pager-bottom-tab-strip:2.3.0'
    implementation 'com.youth.banner:banner:1.4.10'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
    implementation 'cn.jzvd:jiaozivideoplayer:6.1.2'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.8.0'
    implementation 'com.github.yalantis:ucrop:2.2.2'

    // 插件
    compile 'com.qiniu:qiniu-android-sdk:7.3.12'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
